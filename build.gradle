buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.2-2"
    id "org.jetbrains.kotlin.kapt" version "1.1.2-2"
}

group 'com.dlgdev.pokemon'
version '0.1-SNAPSHOT'

ext {
    def daggerVersion = "2.10"

    compileDeps = [
            daggerCompiler: "com.google.dagger:dagger-compiler:$daggerVersion",
    ]

    def jsonVersion = "20080701"
    def commonsVersion = "3.4"
    def gsonVersion = "2.7"
    def mariaDbVersion = "1.5.2"

    runtimeDeps = [
            dagger      : "com.google.dagger:dagger:$daggerVersion",
            kotlinStdlib: "org.jetbrains.kotlin:kotlin-stdlib-jre8",
            json        : "org.json:json:$jsonVersion",
            commons     : "org.apache.commons:commons-lang3:$commonsVersion",
            servletApi  : "/usr/share/jetty9/lib/servlet-api-3.1.jar",
            gson        : "com.google.code.gson:gson:$gsonVersion",
            mariaDb     : "org.mariadb.jdbc:mariadb-java-client:$mariaDbVersion",
    ]

    def jUnitVersion = "4.12"
    def mockitoVersion = "2.8.8"
    testDeps = [
            jUnit          : "junit:junit:$jUnitVersion",
            mockito        : "org.mockito:mockito-core:$mockitoVersion",
            kotlinTest     : "org.jetbrains.kotlin:kotlin-test",
            kotlinTestJunit: "org.jetbrains.kotlin:kotlin-test-junit"
    ]
}

defaultTasks "clean", "build"

allprojects {
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    sourceSets {
        main {
            java.srcDirs += "$buildDir/generated/source/kapt/main"
            output.classesDir = "${rootProject.projectDir}/WebService/WEB-INF/classes"
        }
    }

    repositories {
        jcenter()
    }

    dependencies {
        kapt compileDeps.daggerCompiler
        compile(
                runtimeDeps.dagger,
                runtimeDeps.kotlinStdlib
        )

        testCompile(
                testDeps.jUnit,
                testDeps.mockito,
                testDeps.kotlinTest,
                testDeps.kotlinTestJunit
        )
    }
}